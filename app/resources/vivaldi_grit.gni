import("//tools/grit/grit_rule.gni")
import("//vivaldi/app/resources/untranslated.gni")

template("single_grit_action") {
  assert(defined(invoker.source))
  forward_variables_from(invoker,
                         [
                           "have_strings",
                         ])

  if (!defined(have_strings)) {
    have_strings = false
  }
  deps_list = []

  if (have_strings && generate_untranslated) {
    name = "Extract_strings_$target_name"
    deps_list += [ ":" + name ]
    vivaldi_extract_untranslated(name) {
      source = invoker.source
      source_translations = invoker.source_translations
    }
  }

  name = "grit_$target_name"
  deps_list += [ ":" + name ]
  grit(name) {
    forward_variables_from(invoker, [
                             "inputs",
                             "output_dir",
                             "outputs",
                             "source",
                             "resource_ids",
                           ])
  }

  group(target_name) {
    public_deps = deps_list
  }
}
